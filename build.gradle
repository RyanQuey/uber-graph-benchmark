/*
 *
 *  * Copyright 2018 Uber Technologies Inc.
 *  *
 *  * Licensed under the Apache License, Version 2.0 (the "License");
 *  * you may not use this file except in compliance with the License.
 *  * You may obtain a copy of the License at
 *  *
 *  * http://www.apache.org/licenses/LICENSE-2.0
 *  *
 *  * Unless required by applicable law or agreed to in writing, software
 *  * distributed under the License is distributed on an "AS IS" BASIS,
 *  * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  * See the License for the specific language governing permissions and
 *  * limitations under the License.
 *
 */

import java.text.SimpleDateFormat

plugins {
    id 'net.researchgate.release' version '2.4.0'
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id 'java-library'
    id 'maven'
    id 'maven-publish'
}
apply plugin: 'net.researchgate.release'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'com.uber.ugb'
mainClassName = 'com.uber.ugb.Benchmark'


sourceCompatibility = 1.8

dependencies {
    compile project(':core')
    compile project(':redis')
    compile project(':cassandra')
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

}

task execute(type: JavaExec) {
    main = 'com.uber.ugb.Benchmark'
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty('mainArgs')) {
        args(mainArgs.split(' '))
    }
}

shadowJar {
    // Output to build/libs/<baseName-<version>>.jar
    zip64 true
    baseName = 'ugb-all'
    classifier = null

    // optionally exclude some java librarires
    // exclude 'org.apache.http*'

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    // rename some java librarires
    relocate 'com.google', 'uber.com.google'

    manifest {
        attributes 'Built-Date': (new SimpleDateFormat("yyyy/MM/dd")).format(new Date())
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Build-Jdk': System.getProperty('java.version')
    }
}

shadowDistZip.classifier = 'shadow'
shadowDistTar.classifier = 'shadow'

// the following are uber specific
repositories {
    maven { url 'http://artifactory.uber.internal:4587/artifactory/libs-release-local/' }
    maven { url 'http://artifactory.uber.internal:4587/artifactory/libs-snapshot-local/' }
    mavenCentral()
}

release {
    versionPropertyFile = "$rootDir/gradle.properties"
    tagTemplate = '$name-$version'
}

// ----------------------
// Build & Release
// ----------------------

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            uniqueVersion = false
            def artifactoryUserName = System.getenv('ARTIFACTORY_USERNAME')
            def artifactoryPassword = System.getenv('ARTIFACTORY_PASSWORD')
            if (artifactoryUserName && artifactoryPassword) {
                def remoteRepositoryBucket = project.version.endsWith('-SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
                repository(url: "http://artifactory.uber.internal:4587/artifactory/${remoteRepositoryBucket}/") {
                    authentication(userName: artifactoryUserName, password: artifactoryPassword)
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives
