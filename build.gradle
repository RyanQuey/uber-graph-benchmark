import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "4.0.2"
}

group 'com.uber.ugb'
mainClassName = 'com.uber.ugb.Benchmark'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile project(':core')
    compile project(':redis')
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

}

task execute(type: JavaExec) {
    main = 'com.uber.ugb.Benchmark'
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty('mainArgs')) {
        args(mainArgs.split(' '))
    }
}

shadowJar {
    // Output to build/libs/<baseName-<version>>.jar
    zip64 true
    baseName = 'ugb-all'
    classifier = null

    // optionally exclude some java librarires
    // exclude 'org.apache.http*'

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    // optionally rename some java librarires
    // relocate 'com.google', 'uber.com.google'

    manifest {
        attributes 'Built-Date': (new SimpleDateFormat("yyyy/MM/dd")).format(new Date())
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Build-Jdk': System.getProperty('java.version')
    }
}
